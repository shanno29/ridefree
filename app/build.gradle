apply plugin: "com.android.application"
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: "com.vanniktech.android.junit.jacoco"

android {

    compileSdkVersion rootProject.ext.maxSdk
    kapt.correctErrorTypes true
    dataBinding.enabled true

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        targetSdkVersion rootProject.ext.maxSdk
        minSdkVersion rootProject.ext.minSdk
        applicationId rootProject.ext.appName
        multiDexEnabled true
        versionCode 1000
        versionName "1"
        resConfigs "en"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/ASL2.0"
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/INDEX.LIST"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/rxjava.properties"
        exclude "META-INF/lib_main.kotlin_module"
        exclude "META-INF/io.netty.versions.properties"
    }

    signingConfigs {

        debug {
            keyPassword "android"
            storePassword "android"
            keyAlias "androiddebugkey"
            storeFile file("keystore/debug.keystore")

        }

        release {
            keyAlias "alias"
            keyPassword "$System.env.APP_KEY"
            storePassword "$System.env.APP_KEY"
            storeFile file("keystore/release.keystore")
        }
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            //useProguard true
            versionNameSuffix "Debug"
            testCoverageEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            testCoverageEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }

    testOptions.unitTests {
        includeAndroidResources = true
        returnDefaultValues = true
        all { testLogging.events 'passed', 'skipped', 'failed', 'standardOut', 'standardError' }
    }

    junitJacoco {
        jacocoVersion = '0.7.2.201409121644'
        includeNoLocationClasses = true
        excludes = [
            '**/*Test*.*',
            '**/AutoValue_*.*',
            '**/*_Impl*.*',
            '**/*_*.*',
            '**/*AjcClosure*',
            '**/*JavascriptBridge.class',
            'android/**/*.*',
            '**/*$InjectAdapter.class',
            '**/*$ModuleAdapter.class',
            '**/*$ViewInjector*.class',
            '**/*dataBinding/*.*',
            '**/com/wiscosoft/ridefree/databinding/*',
            '**/android/databinding/*',
            '**/*_MembersInjector*.*',
            '**/*BR*.*',
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*$Lambda$*.*',
            '**/Lambda$*.class',
            '**/Lambda.class',
            '**/*Lambda.class',
            '**/*Lambda*.class',
            '**/*Module.*',
            '**/*Component*.*',
            '**/*Dagger*.*',
            '**/*MembersInjector*.*',
            '**/*_Provide*Factory*.*',
            '**/*_Factory.*',
            '**/*$*$*.*',
            '**/Epoxy*',
            '**/*equals(Object)*'
        ]
    }

}


dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.ext.kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$rootProject.ext.kotlin"

    // Android
    kapt "com.android.databinding:compiler:$rootProject.ext.gradle"
    implementation "com.android.support:design:$rootProject.ext.support"
    implementation "com.android.support:cardview-v7:$rootProject.ext.support"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.support"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.support"

    // Google (Gps & Maps)
    implementation "com.google.android.gms:play-services-maps:$rootProject.ext.gsm"
    implementation "com.google.android.gms:play-services-location:$rootProject.ext.gsm"

    // UI
    implementation 'com.github.d-max:spots-dialog:0.7@aar'
    implementation 'com.braintreepayments:card-form:3.0.5'
    implementation('com.mikepenz:aboutlibraries:6.0.1@aar') { transitive true }
    implementation 'com.github.Ilhasoft:data-binding-validator:0.6.4'

    // ROOM (SQL Database)
    implementation "android.arch.persistence.room:runtime:$rootProject.ext.room"
    implementation "android.arch.persistence.room:rxjava2:$rootProject.ext.room"
    kapt "android.arch.persistence.room:compiler:$rootProject.ext.room"

    // Epoxy (RecyclerView)
    implementation "com.airbnb.android:epoxy-databinding:$rootProject.ext.epoxy"
    implementation "com.airbnb.android:epoxy:$rootProject.ext.epoxy"
    kapt "com.airbnb.android:epoxy-processor:$rootProject.ext.epoxy"

    // Retrofit (Networking)
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttp"

    // Redux (State Management)
    implementation "com.github.pardom:redux-kotlin:$rootProject.ext.redux"
    implementation "com.github.pardom:redux-logger-kotlin:$rootProject.ext.reduxExtra"
    implementation "com.github.pardom.redux-rxjava:redux-rxjava2:$rootProject.ext.reduxExtra"

    // RxJava (Async & Threading)
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxAndroid"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxJava"
    implementation "io.reactivex.rxjava2:rxkotlin:$rootProject.ext.rxKotlin"
    implementation 'com.jakewharton.rxbinding2:rxbinding-design:2.0.0'
    implementation 'com.patloew.rxlocation:rxlocation:1.0.3'
    implementation 'com.github.raviteja06:RxPermissions:1.2.0'
    implementation 'com.github.miguelbcr:RxPaparazzo:0.6.0-2.x'

    // Dagger 2 (Dependancy Injection)
    implementation "com.google.dagger:dagger:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"

    testImplementation "com.google.dagger:dagger:$rootProject.dagger"
    testImplementation "com.google.dagger:dagger-android:$rootProject.dagger"
    testImplementation "com.google.dagger:dagger-android-support:$rootProject.dagger"

    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Unit Tests
    testImplementation "junit:junit:$rootProject.ext.junit"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockito"
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectric"
    testImplementation 'org.robolectric:shadows-support-v4:3.3.2'

    // UI Tests
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.testrunner"
    androidTestImplementation "com.android.support.test:rules:$rootProject.ext.testrunner"
    androidTestImplementation "com.android.support:support-annotations:$rootProject.ext.support"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.ext.espresso"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.ext.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

}
