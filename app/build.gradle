apply plugin: "com.android.application"
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.vanniktech.android.junit.jacoco"

kapt.correctErrorTypes true

android {
    buildToolsVersion rootProject.ext.buildTools
    compileSdkVersion rootProject.ext.maxSdk
    dataBinding.enabled true

    defaultConfig {
        targetSdkVersion rootProject.ext.maxSdk
        minSdkVersion rootProject.ext.minSdk
        applicationId rootProject.ext.appName
        versionCode 1000
        versionName "1"
        resConfigs "en"
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/ASL2.0"
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/INDEX.LIST"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/rxjava.properties"
        exclude "META-INF/lib_main.kotlin_module"
        exclude "META-INF/io.netty.versions.properties"
    }

    signingConfigs {

        debug {
            keyPassword "android"
            storePassword "android"
            keyAlias "androiddebugkey"
            storeFile file("keystore/debug.keystore")

        }

        release {
            keyAlias "alias"
            keyPassword "$System.env.APP_KEY"
            storePassword "$System.env.APP_KEY"
            storeFile file("keystore/release.keystore")
        }
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix "Debug"
            testCoverageEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }

    junitJacoco {
        jacocoVersion = '0.7.2.201409121644'
        includeNoLocationClasses = true
        excludes = [
            '**/*_*.*',
            '**/*BR*.*',
            '**/R.class',
            '**/*_Impl*.*',
            '**/R$*.class',
            '**/*Module*.*',
            '**/Manifest*.*',
            'android/**/*.*',
            '**/*AjcClosure*',
            '**/Lambda.class',
            '**/BuildConfig.*',
            '**/*_*Factory*.*',
            '**/*Component*.*',
            '**/AutoValue_*.*',
            '**/*Lambda.class',
            '**/Lambda$*.class',
            '**/*Lambda*.class',
            '**/*$ViewBinder*.*',
            '**/*$ViewInjector*.*',
            '**/*_MembersInjector*.*',
            '**/*$InjectAdapter.class',
            '**/*$ModuleAdapter.class',
            '**/*$ViewInjector*.class',
            '**/*_MembersInjector.class',
            '**/*JavascriptBridge.class',
        ]
    }

    testOptions.unitTests {
        includeAndroidResources true
        returnDefaultValues true
        all { testLogging.events 'passed', 'skipped', 'failed', 'standardOut', 'standardError' }
    }

}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.ext.kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$rootProject.ext.kotlin"

    // Android
    kapt "com.android.databinding:compiler:$rootProject.ext.gradle"
    implementation "com.android.support:design:$rootProject.ext.support"
    implementation "com.android.support:cardview-v7:$rootProject.ext.support"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.support"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.support"

    // Google (Gps & Maps)
    implementation "com.google.android.gms:play-services-maps:$rootProject.ext.gsm"
    implementation "com.google.android.gms:play-services-location:$rootProject.ext.gsm"

    // ROOM (SQL Database)
    kapt "android.arch.persistence.room:compiler:$rootProject.ext.room"
    implementation "android.arch.persistence.room:runtime:$rootProject.ext.room"
    implementation "android.arch.persistence.room:rxjava2:$rootProject.ext.room"

    // Retrofit (Networking)
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttp"

    // Kodein (Dependency Injection)
    implementation "com.github.salomonbrys.kodein:kodein:$rootProject.ext.kodein"
    implementation "com.github.salomonbrys.kodein:kodein-android:$rootProject.ext.kodein"

    // Redux (State Management)
    implementation "com.github.pardom:redux-kotlin:$rootProject.ext.redux"
    implementation "com.github.pardom:redux-logger-kotlin:$rootProject.ext.reduxExtra"
    implementation "com.github.pardom.redux-rxjava:redux-rxjava2:$rootProject.ext.reduxExtra"

    // UI
    implementation "com.github.d-max:spots-dialog:0.7@aar"
    implementation "com.braintreepayments:card-form:3.0.5"
    implementation("com.mikepenz:aboutlibraries:6.0.1@aar") { transitive true }

    // RxJava (Async & Threading)
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxjava"
    implementation "io.reactivex.rxjava2:rxkotlin:$rootProject.ext.rxkotlin"
    implementation "com.patloew.rxlocation:rxlocation:1.0.3"
    implementation "com.github.raviteja06:RxPermissions:1.2.0"
    implementation "com.github.miguelbcr:RxPaparazzo:0.6.0-2.x"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:2.0.0"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3"

    // Unit Tests
    testImplementation "junit:junit:$rootProject.ext.junit"
    testImplementation "org.mockito:mockito-core:1.10.19"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.roboElectric"
    testImplementation "org.robolectric:shadows-support-v4:3.0"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2") {
        exclude group: "com.android.support", module: "support-annotations"
    }

}

